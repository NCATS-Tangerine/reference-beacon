// NCATS Translator Knowledge.Bio (TKBio) Main Meta-Project Root Build File

ext {
	gradleVersion            = '4.7'
	apacheHTTPClientVersion  = '4.5.2'
	springCoreVersion        = '5.0.4.RELEASE'
	jacksonXmlVersion        = '2.9.4'
	logbackCoreVersion       = '1.2.3'
	springBootVersion        = '2.0.0.RC1'
	springDataVersion        = 'Kay-SR3'
	springDataNeo4jVersion   = '5.0.4.RELEASE'
	springNeo4jVersion       = '3.3.4'
	springNeo4jOGMVersion    = '3.0.3'
	springfoxSwaggerVersion  = '2.5.0'
}

buildscript {
    ext {
        springBootVersion = '2.0.0.RC1'
        springDependencyMgmtVersion = '1.0.4.RELEASE'
    }
    repositories {
        mavenCentral()
		maven { url "https://repo.spring.io/libs-release" }
		maven { url 'https://repo.spring.io/libs-milestone' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion") 
        classpath("io.spring.gradle:dependency-management-plugin:$springDependencyMgmtVersion")
        classpath("com.eriwen:gradle-css-plugin:2.14.0")
    }
}

allprojects {

	// Version 4.0 now uses the new Translator Knowledge Beacon API
	
	// This release implements the Translator Knowledge Beacon version 1.0.17 API
	version = '1.0.17'  
	
    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'eclipse-wtp'
    apply plugin: 'io.spring.dependency-management'
		
	group = 'bio.knowledge'
	
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
    
    repositories {
	    mavenCentral()
	    mavenLocal()
	    maven { url "https://repo.spring.io/libs-release" }
	    maven { url 'http://repo.spring.io/milestone' }
	    maven { url 'http://repo.spring.io/libs-snapshot' }
	    maven { url "https://m2.neo4j.org" }
	    maven { url "http://mvnrepository.com/" }
    }
    
    uploadArchives {
	    repositories {
	        mavenDeployer {
	            pom {
	                project {
	                    parent {
	                        groupId "org.springframework.boot"
	                        artifactId "spring-boot-starter-parent"
	                        version "$springBootVersion"
	                    }
	                }
	            }
	        }
	    }
	}
    
    dependencies {

    	compile "commons-collections:commons-collections:3.2"
		compile 'org.apache.commons:commons-lang3:3.4'
		compile "org.apache.httpcomponents:httpclient:$apacheHTTPClientVersion"
				
		compile "com.fasterxml.jackson.core:jackson-core:$jacksonXmlVersion"
		compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonXmlVersion"
		compile "com.fasterxml.jackson.core:jackson-databind:$jacksonXmlVersion"
				
		compile "ch.qos.logback:logback-core:$logbackCoreVersion"
		compile "ch.qos.logback:logback-classic:$logbackCoreVersion"
		compile "ch.qos.logback:logback-access:$logbackCoreVersion"

    	compile "org.springframework:spring-core:$springCoreVersion"
    	compile "org.springframework:spring-context:$springCoreVersion"
    	compile "org.springframework:spring-context-support:$springCoreVersion"
    	compile "org.springframework:spring-aop:$springCoreVersion"
    	compile "org.springframework:spring-beans:$springCoreVersion"
    	compile "org.springframework:spring-expression:$springCoreVersion"
    	compile "org.springframework:spring-test:$springCoreVersion"
    	compile "org.springframework:spring-tx:$springCoreVersion"
    	compile "org.springframework:spring-web:$springCoreVersion"
    	compile "org.springframework:spring-webmvc:$springCoreVersion"
    	
    	compile "org.springframework.boot:spring-boot:$springBootVersion"
    	compile "org.springframework.boot:spring-boot-actuator:$springBootVersion"
    	compile "org.springframework.boot:spring-boot-autoconfigure:$springBootVersion"
    	
    	compile "org.springframework.boot:spring-boot-starter:$springBootVersion"
    	compile "org.springframework.boot:spring-boot-starter-logging:$springBootVersion"
    	compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    	compile "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
    	//compile "org.springframework.boot:spring-boot-starter-mail:$springBootVersion"
   		compile "org.springframework.boot:spring-boot-starter-tomcat:$springBootVersion"
		compile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
 
	   	compile "org.neo4j:neo4j-codegen:$springNeo4jVersion"
	   	compile "org.neo4j:neo4j:$springNeo4jVersion"
	    compile "org.neo4j:neo4j-kernel:$springNeo4jVersion"
	    compile "org.neo4j:neo4j-io:$springNeo4jVersion"
	    
	    compile "org.neo4j:neo4j-ogm-core:$springNeo4jOGMVersion"
	    compile "org.neo4j:neo4j-ogm-http-driver:$springNeo4jOGMVersion"
	    compile "org.neo4j:neo4j-ogm-embedded-driver:$springNeo4jOGMVersion"
	    compile "org.neo4j:neo4j-ogm-bolt-driver:$springNeo4jOGMVersion"

    	compile "org.springframework.data:spring-data-neo4j:$springDataNeo4jVersion"

		testCompile 'junit:junit:4.12'
   }
    
	dependencyManagement {
		imports { 
			mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Angel.SR3" 
			mavenBom "org.springframework:spring-framework-bom:${springCoreVersion}"
			mavenBom 'org.springframework.data:spring-data-releasetrain:Hopper-SR1'
		}
	}
		
    jar {
        from sourceSets.main.allSource
        manifest.attributes provider: 'STAR Informatics'
    }
}

configurations {
    providedRuntime
    all*.exclude group: '', module: 'servlet-api'
}

configurations.all {
    resolutionStrategy.eachDependency {
        if(it.requested.name == 'org.objectweb.asm') {
            it.useTarget 'org.ow2.asm:asm:5.0.3'
        }
    }
}

eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "$gradleVersion"
}

project(':models') {

	archivesBaseName  = 'rkb-models'
	description       = 'NCATS Translator Reference Beacon (RKB) Domain Model Interfaces and Classes'

	evaluationDependsOn(':ontology')

	dependencies {
		compile project(':ontology')
		compile("org.springframework.data:spring-data-neo4j:$springDataNeo4jVersion")
	}
}

project(':database') {

	archivesBaseName  = 'rkb-database'
	description       = 'NCATS Translator Reference Beacon (RKB) Neo4j Database Middleware'
	
	evaluationDependsOn(':models')

	dependencies {
  
		compile project(':models')
		
		compile("org.springframework.boot:spring-boot:$springBootVersion")
		compile("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
		
		compile("org.springframework.data:spring-data-neo4j:$springDataNeo4jVersion")
		compile "org.neo4j:neo4j-codegen:$springNeo4jVersion"
		compile "org.neo4j:neo4j:$springNeo4jVersion"
		compile "org.neo4j:neo4j-kernel:$springNeo4jVersion"
		compile "org.neo4j:neo4j-io:$springNeo4jVersion"
			
		compile("org.neo4j:neo4j-ogm-core:$springNeo4jOGMVersion")
		compile "org.neo4j:neo4j-ogm-http-driver:$springNeo4jOGMVersion"
		compile "org.neo4j:neo4j-ogm-embedded-driver:$springNeo4jOGMVersion"
		compile "org.neo4j:neo4j-ogm-bolt-driver:$springNeo4jOGMVersion"
  }
}

project(':server') {

	archivesBaseName  = 'reference-beacon'
	description       = 'Reference Knowledge Beacon Using Knowledge.Bio 3.0 Database Access'
	
    apply plugin: 'org.springframework.boot'
	
	evaluationDependsOn(':database')
	
	dependencies {
	
		compile project(':database')
	
    	compile "org.springframework:spring-context:$springCoreVersion"
    	compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
		compile "org.springframework.boot:spring-boot-starter-parent:$springBootVersion"
		
	    compile("io.springfox:springfox-swagger2:$springfoxSwaggerVersion")
	    compile("io.springfox:springfox-swagger-ui:$springfoxSwaggerVersion")
	
	    // https://mvnrepository.com/artifact/com.fasterxml.jackson.datatype/jackson-datatype-joda
	    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-joda', version: '2.9.0.pr2'
	
	    // https://mvnrepository.com/artifact/joda-time/joda-time
	    compile group: 'joda-time', name: 'joda-time', version: '2.9.9'
	
	    // https://mvnrepository.com/artifact/javax.validation/validation-api
	    compile group: 'javax.validation', name: 'validation-api', version: '1.1.0.Final'
	    
		// https://mvnrepository.com/artifact/io.swagger/swagger-jaxrs
		compile group: 'io.swagger', name: 'swagger-jaxrs', version: '1.5.0'
	
	    compile 'javax.validation:validation-api:1.1.0.Final'
	    
	    compile 'org.hibernate:hibernate-validator:5.2.2.Final'
	    compile 'commons-cli:commons-cli:1.1'
	}
	
	jar {
	    from sourceSets.main.allSource
	    manifest {
	        attributes("Main-Class": "bio.knowledge.server.Swagger2SpringBoot")
	        attributes("provider": "STAR Informatics")
	    }
	}
	
	//springBoot {
    //	mainClassName = "bio.knowledge.server.Swagger2SpringBoot"
	//}
}

evaluationDependsOn(':server')

dependencies {
    compile project(':server')
}